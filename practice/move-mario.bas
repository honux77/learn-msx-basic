15 DRAW MARIO
20 DEFINT A-Z:SCREEN 1,3:X=0:Y=60
30 LOCATE 2,14:PRINT"HELLO, MARIO"
40 S=BASE(9)
50 READ R$:IF R$<>"*" THEN VPOKE S,VAL("&H"+R$):S=S+1:GOTO 50
60 ' --- Slot 0
70 ' color 1
80 DATA 00,00,00,07,0A,0B,0C,00,3E,7F,3F,02,10,38,70,40
90 DATA 00,00,00,27,27,11,3E,04,E8,71,01,03,03,03,00,00
100 ' color 6
110 DATA 00,03,07,00,00,00,00,00,01,00,00,0D,0F,07,0F,0F
120 DATA 00,E0,FC,00,00,00,00,00,10,88,F8,B4,FC,FC,E0,00
130 ' color 11
140 DATA 00,00,00,00,05,04,03,03,00,00,C0,E0,40,00,00,00
150 DATA 07,07,03,D0,D8,EE,C0,F8,00,00,00,48,00,00,00,00
160 DATA *
170 PUT SPRITE 0,(X,Y),1,0:PUT SPRITE 1,(X,Y),6,1:PUT SPRITE 2,(X,Y),11,2
180 FORI=BASE(8)TOBASE(8)+127:VPOKE128+I,VPEEK(I):NEXT
190 Q=BASE(8):A=Q/128:W=255:Z=128:VDP(5)=A
200 LOCATE 2,15:PRINT"PRESS 1, 2 OR ESC ";CHR$(27);"K";:A$=INPUT$(1):IF A$=CHR$(27) THEN SCREEN 0:END ELSE LOCATE 2,15:PRINT"PRESS ANY KEY TO RESTART":IF A$="1" THEN 230
210 ' USE SIMPLE LOOP
220 FORI=0TO20:NEXT:VPOKEQ+1,X:VPOKEQ+5,X:VPOKEQ+9,X:X=X+2ANDW:IFINKEY$=""THEN220ELSE190
230 ' USE VDP INTERRUPT
240 ON INTERVAL=2 GOSUB 260:INTERVALON
250 IF INKEY$="" THEN 250 ELSE INTERVALOFF:GOTO 190
260 VDP(5)=A:A=AXOR1:Q=QXORZ:VPOKEQ,Y:VPOKEQ+1,X:VPOKEQ+5,X:VPOKEQ+9,X:X=X+2ANDW:RETURN
